
/////////////////
/// Entrypoint
/////////////////
body = _{ properties? ~ import_statement ~ documents ~ EOI }

properties = _{ SOI ~ "---" ~ NEWLINE ~ !"---"+ ~ NEWLINE ~ "---" ~ NEWLINE+ }

import_statement = { "IMPORT" ~ path ~ ("," ~ path)* ~ NEWLINE+ }

path_char = { "_" | "." | "/" | 'a'..'z' | 'A'..'Z' | '0'..'9' }

path = @{ "\"" ~ path_char+ ~ "\"" }

documents = { document ~ (NEWLINE+ ~ document)* ~ NEWLINE* }

document = { title ~ NEWLINE+ ~ schema ~ NEWLINE+ ~ outputs }

title = { "#" ~ TEXT ~ NEWLINE }

schema_tag = { "## Schema" ~ NEWLINE }

schema = { schema_tag ~ table }


////////////
/// TABLE
////////////

// It'd be nice if I could use the stack to enforce that the table isn't jagged
table = { heading ~ row+ }

heading = { column_names ~ NEWLINE ~ table_divider ~ NEWLINE }

column_names = { ("|" ~ TEXT)+ ~ "|" }

table_divider = _{ ("|" ~ column_divider)+ ~ "|" }

column_divider = @{ ":"? ~ "-"+ ~ ":"? }

row = { ("|" ~ (mockagen_id_and_metadata | TEXT))+ ~ "|" ~ NEWLINE }



//////////////
/// OUTPUTS
//////////////

OUTPUTS_TAG = _{ "## Outputs" ~ NEWLINE }

outputs = { OUTPUTS_TAG ~ output_type+ }

output_type = { output_tabular | output_document }


output_tabular = { OUTPUT_TABULAR_TAG ~ output_tabular_formats ~ output_tabular_column_names ~ output_tabular_row_values }

OUTPUT_TABULAR_TAG = _{
    " - ### Tabular" ~ NEWLINE
}

output_tabular_formats = { OUTPUT_TABULAR_FORMATS_TAG ~ output_tabular_format_type_indented+ }

OUTPUT_TABULAR_FORMATS_TAG = _{
    "    - Formats" ~ NEWLINE
}

output_tabular_format_type_indented = { "        - " ~ output_tabular_format_type ~ NEWLINE }

output_tabular_format_type = { "CSV" | "TSV" }

output_tabular_column_names = { OUTPUT_TABULAR_COLUMN_NAMES_TAG ~ indented_x4_text+ }



OUTPUT_TABULAR_COLUMN_NAMES_TAG = _{
    "    - Column names" ~ NEWLINE
}

output_tabular_row_values = { OUTPUT_TABULAR_ROW_VALUES_TAG ~ indented_x4_text+ }

OUTPUT_TABULAR_ROW_VALUES_TAG = _{
    "    - Row values" ~ NEWLINE
}


output_document = { OUTPUT_DOCUMENT_TAG ~ output_document_formats ~ output_document_members }

OUTPUT_DOCUMENT_TAG = _{
    " - ### Document" ~ NEWLINE
}

output_document_formats = { OUTPUT_DOCUMENT_FORMATS_TAG ~ output_document_format_indented+ }

output_document_format_indented = { "        - " ~ output_document_format ~ NEWLINE }

output_document_format = { "JSON" }

OUTPUT_DOCUMENT_FORMATS_TAG = _{
    "   - Formats" ~ NEWLINE
}

output_document_members = { OUTPUT_DOCUMENT_MEMBERS_TAG ~ indented_x4_text+ ~ NEWLINE }

OUTPUT_DOCUMENT_MEMBERS_TAG = _{
    "   - Members" ~ NEWLINE
}

indented_x4_text = { "        - " ~ TEXT ~ NEWLINE }

////////////////
/// TERMINALS
////////////////

mockagen_id_and_metadata = { mockagen_identifier ~ "AS" ~ METADATA_PROPERTIES }

METADATA_PROPERTIES =
    { PRIMARY_TIMESTAMP ~ "," ~ PERSONAL
    | PRIMARY_TIMESTAMP
    | PERSONAL
    }

PRIMARY_TIMESTAMP = { "PRIMARY TIMESTAMP" }

PERSONAL = { "PERSONAL" }

mockagen_identifier = ${ GENERATOR_MARKER ~ MOCKAGEN_IDENTIFIER ~ GENERATOR_MARKER }

TEXT = @{ (ASCII_ALPHA | "_" | " ")+ }

MOCKAGEN_IDENTIFIER = @{ (ASCII_ALPHA_LOWER | "-")+ }

GENERATOR_MARKER = _{ "`" }

WHITESPACE = _{ " " }
