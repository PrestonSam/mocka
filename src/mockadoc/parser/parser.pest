body = _{ properties? ~ import_statement ~ documents ~ EOI }

properties = _{ SOI ~ "---" ~ NEWLINE ~ !"---"+ ~ NEWLINE ~ "---" ~ NEWLINE+ }

import_statement = { "IMPORT" ~ path ~ ("," ~ path)* ~ NEWLINE+ }

path = { "\"" ~ !"\""+ ~ "\"" }

documents = { document ~ (NEWLINE+ ~ document)* ~ NEWLINE* }

document = { title ~ NEWLINE+ ~ table }

title = { "### " ~ TEXT }

// It'd be nice if I could use the stack to enforce that the table isn't jagged
table = { heading ~ (NEWLINE ~ row)+ }

heading = { column_names ~ NEWLINE ~ table_divider }

column_names = { ("|" ~ (METADATA_TAG | GENERATOR_TAG | DATA_KEY | TEXT))+ ~ "|" }

table_divider = _{ ("|" ~ column_divider)+ ~ "|" }

column_divider = @{ ":"? ~ "-"+ ~ ":"? }

row = { ("|" ~ (METADATA_PROPERTIES | mockagen_identifier | TEXT))+ ~ "|" }

METADATA_PROPERTIES =
    { PRIMARY_TIMESTAMP ~ PERSONAL
    | PRIMARY_TIMESTAMP
    | PERSONAL
    }

PRIMARY_TIMESTAMP = { "PRIMARY TIMESTAMP" }

PERSONAL = { "PERSONAL" }

mockagen_identifier = ${ GENERATOR_MARKER ~ MOCKAGEN_IDENTIFIER ~ GENERATOR_MARKER }

TEXT = @{ ASCII_ALPHA ~ (ASCII_ALPHA | " " | "_")* ~ &("|" | NEWLINE) }

MOCKAGEN_IDENTIFIER = @{ (ASCII_ALPHA_LOWER | "-")+ }

DATA_KEY =  @{ ITALICS_MARKER ~ TEXT ~ ITALICS_MARKER }

GENERATOR_TAG = @{ GENERATOR_MARKER ~ "GENERATOR" ~ GENERATOR_MARKER }

METADATA_TAG = @{ GENERATOR_MARKER ~ "METADATA" ~ GENERATOR_MARKER }

GENERATOR_MARKER = _{ "`" }

ITALICS_MARKER = _{ "*" | "_" }
