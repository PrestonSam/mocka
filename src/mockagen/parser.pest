
body = _{ SOI ~ NEWLINE* ~ (include_statement ~ NEWLINE+)? ~ statements ~ NEWLINE* ~ EOI }

include_statement = { "INCLUDE" ~ STRING_LITERAL ~ ("," ~ STRING_LITERAL)* }

statements = _{ definition ~ (NEWLINE+ ~ definition)* }

definition =
   _{ single_definition
    | nested_definition
    }


single_definition =
    { ( "DEF" ~ IDENTIFIER ~ "=" ~ value )
    | ( "DEF" ~ IDENTIFIER ~ NEWLINE ~ PUSH(TAB) ~ "=" ~ value_set ~ DROP )
    }


nested_definition = { using_ids? ~ "DEF" ~ assign_ids ~ nested_clauses }


using_ids = { "USING" ~ names }

assign_ids = { names }

names = _{ IDENTIFIER ~ ("," ~ IDENTIFIER)* }


nested_clauses = { ((NEWLINE ~ match)+ | (NEWLINE ~ assign)+ ) }

match = { PEEK_ALL ~ PUSH(TAB+) ~ "?" ~ matchers ~ (match_sub_assignments | match_sub_matches) ~ DROP }

match_sub_assignments = { (NEWLINE ~ assign)+ }

match_sub_matches = { (NEWLINE ~ match)+ }

matchers =
    { value
    | matcher_set
    }

matcher_set = { "ONEOF" ~ (NEWLINE ~ PEEK_ALL ~ "|" ~ value)+ }


assign = { PEEK_ALL ~ PUSH(TAB+) ~ "=" ~ weighted_values ~ sub_assignments ~ DROP }

sub_assignments = { (NEWLINE ~ assign)* }

weighted_values = { WEIGHT? ~ values }

values =
    { value
    | value_set
    }

value_set = { "ONEOF" ~ (NEWLINE ~ PEEK_ALL ~ "|" ~ weighted_value)+ }


weighted_value = { WEIGHT? ~ value }





value =
    { timestamp_date_value
    | literal_value
    | integer_value
    | string_value
    | real_value
    | join_value
    | any_value
    | identifier_value
    }

timestamp_date_value = { "timestamp/date" ~ DATE_LITERAL ~ DATE_LITERAL }

literal_value = { STRING_LITERAL }

integer_value = { "integer" ~ INTEGER_LITERAL ~ INTEGER_LITERAL? }

string_value = { "string" ~ INTEGER_LITERAL ~ INTEGER_LITERAL }

real_value = { "real" ~ REAL_LITERAL ~ REAL_LITERAL? }

join_value = { "join" ~ (identifier_value | literal_value)+ }

any_value = { "any" }

identifier_value = { IDENTIFIER }


PERCENTAGE_NUMBER = { ASCII_NONZERO_DIGIT ~ ASCII_DIGIT? }

DECIMAL_SUFFIX =
    { ASCII_NONZERO_DIGIT
    | (ASCII_DIGIT ~ ASCII_NONZERO_DIGIT)
    | (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT)
    }

DECIMAL_PERCENTAGE_NUMBER =
    { PERCENTAGE_NUMBER?
    ~ "."
    ~ DECIMAL_SUFFIX
    }

WEIGHTING =
   _{ (PERCENTAGE_NUMBER ~ !".")
    | DECIMAL_PERCENTAGE_NUMBER
    | "100" // I should remove this as a language feature. You should just omit the weight.
    }

WEIGHT = ${ WEIGHTING ~ "%" }

DATE_LITERAL = @{ ASCII_DIGIT{4} ~ "-" ~ ASCII_DIGIT{2} ~ "-" ~ ASCII_DIGIT{2} }

REAL_LITERAL = @{ INTEGER_LITERAL ~ "." ~ ASCII_DIGIT+ }

INTEGER_LITERAL = @{ "-"? ~ ASCII_DIGIT+ }

SYNTAX_KEYWORDS =
    { "INCLUDE"
    | "ONEOF"
    | "USING"
    | "DEF"
    }

TYPE_KEYWORDS =
    { "timestamp/date"
    | "integer"
    | "real"
    | "string"
    | "join"
    | "any"
    }

RESERVED_WORDS = { SYNTAX_KEYWORDS | TYPE_KEYWORDS }

IDENTIFIER = @{ &!RESERVED_WORDS ~ (ASCII_ALPHA | "-")+ }

STRING_LITERAL = ${ "\"" ~ string_content ~ "\"" }
string_content = @{ string_char* }
string_char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

TAB = @{ " "{4} }

WHITESPACE = _{ &!TAB ~ " " }
